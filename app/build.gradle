apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'hugo'


// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
  compileSdkVersion 25
  buildToolsVersion "26.0.0"
  defaultConfig {
    applicationId "kr.co.enterprise1.mfpdemo"
    minSdkVersion 16
    targetSdkVersion 25
    versionCode 4
    versionName "1.3"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  signingConfigs {
    release {
      keyAlias keystoreProperties['keyAlias']
      storePassword keystoreProperties['storePassword']
      keyPassword keystoreProperties['keyPassword']
      storeFile file(keystoreProperties['storeFile'])
    }
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.release
      applicationVariants.all { variant ->
        variant.outputs.each { output ->
          def formattedDate = new Date().format('yyyyMMdd_HHmmss')
          def apkName = "MemoryPower_" + variant.versionName +
              "_" +
              variant.versionCode +
              "_" +
              formattedDate +
              "_release.apk"
          output.outputFile = new File(output.outputFile.parent, apkName)
        }
      }
      debug {}
    }
  }
  packagingOptions {
    pickFirst 'META-INF/ASL2.0'
    pickFirst 'META-INF/LICENSE'
    pickFirst 'META-INF/NOTICE'
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testCompile 'junit:junit:4.12'

  compile "com.android.support:appcompat-v7:$rootProject.ext.supportLibVer"
  compile "com.android.support:design:$rootProject.ext.supportLibVer"
  compile "com.android.support:preference-v7:$rootProject.ext.supportLibVer"
  compile 'com.android.support.constraint:constraint-layout:1.0.2'

  compile 'com.ibm.mobile.foundation:ibmmobilefirstplatformfoundation:8.0.+'
  compile 'com.ibm.mobile.foundation:ibmmobilefirstplatformfoundationpush:8.0.+'
  compile 'com.google.android.gms:play-services-gcm:9.6.1'

  compile "com.jakewharton:butterknife:$rootProject.ext.butterKnifeVer"
  annotationProcessor "com.jakewharton:butterknife-compiler:$rootProject.ext.butterKnifeVer"

  compile 'org.projectlombok:lombok:1.16.16'
  provided 'javax.annotation:jsr250-api:1.0'

  compile 'com.pixplicity.easyprefs:library:1.8.1@aar'
  compile 'com.facebook.stetho:stetho:1.5.0'

  compile 'com.google.code.gson:gson:2.8.1'

  compile 'com.github.ybq:Android-SpinKit:1.1.0'

  compile 'com.squareup:otto:1.3.8'
}